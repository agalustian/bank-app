import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id "java"
    id "org.springframework.boot" version "3.5.6"
    id "io.spring.dependency-management" version "1.1.7"
    id "org.openapi.generator" version "7.12.0"
}

group = "ru.bank"
version = "0.0.1"
description = "Bank accounts application"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', "$projectDir/build/generated/src/main/java"]
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.liquibase:liquibase-core:4.25.1'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13'

    runtimeOnly 'org.postgresql:postgresql'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.springframework.boot:spring-boot-testcontainers"
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation "org.testcontainers:postgresql"

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named("test") {
    useJUnitPlatform {
        filter {
            exclude("**/integration/**")
        }
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.register("integrationTest", Test) {
    useJUnitPlatform {
        filter {
            include("**/integration/**")
        }
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.register("buildNotificationsClient", GenerateTask) {
    generatorName.set("spring")
    inputSpec.set("$projectDir/src/main/resources/notifications.json")
    outputDir.set("$projectDir/build/generated")
    modelPackage.set("ru.bank.accounts.notifications.domain")
    apiPackage.set("ru.bank.accounts.notifications..api")
    configOptions.set([
            sourceFolder: "src/gen/java/main/codegen",
            interfaceOnly: "false",
            library: "spring-boot",
            useTags: "true"
    ])
}

compileJava.dependsOn tasks.buildNotificationsClient
