import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id "java"
    id "org.springframework.boot" version "3.5.6"
    id "io.spring.dependency-management" version "1.1.7"
    id "org.openapi.generator" version "7.12.0"
}

group = "ru.bank"
version = "0.0.1"
description = "Bank front ui application"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

sourceSets.main.java.srcDir "$projectDir/build/generated/src/main/java"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'ru.bank:microservices-chassis:0.0.14'

    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-client"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named("test") {
    useJUnitPlatform {
        filter {
            exclude("**/integration/**")
        }
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.register("integrationTest", Test) {
    useJUnitPlatform {
        filter {
            include("**/integration/**")
        }
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.register("buildAccountsClient", GenerateTask) {
    generatorName.set("java")
    inputSpec.set("$projectDir/src/main/resources/accounts.json")
    outputDir.set("$projectDir/build/generated")
    modelPackage.set("ru.bank.front.accounts.domain")
    invokerPackage.set("ru.bank.front.accounts.client")
    apiPackage.set("ru.bank.front.accounts.client.api")
    packageName.set("Accounts")
    configOptions.set([
            hideGenerationTimestamp: "true",
            library                : "resttemplate",
            useJakartaEe           : "true",
            useTags                : "true",
            openApiNullable        : "false",
            serializableModel      : "true"
    ])
}

compileJava.dependsOn tasks.buildAccountsClient

tasks.register("buildTransferClient", GenerateTask) {
    generatorName.set("java")
    inputSpec.set("$projectDir/src/main/resources/transfer.json")
    outputDir.set("$projectDir/build/generated")
    modelPackage.set("ru.bank.front.transfer.domain")
    invokerPackage.set("ru.bank.front.transfer.client")
    apiPackage.set("ru.bank.front.transfer.client.api")
    packageName.set("Transfer")
    configOptions.set([
            hideGenerationTimestamp: "true",
            library                : "resttemplate",
            useJakartaEe           : "true",
            useTags                : "true",
            openApiNullable        : "false",
            serializableModel      : "true"
    ])
}

compileJava.dependsOn tasks.buildTransferClient

tasks.register("buildCashClient", GenerateTask) {
    generatorName.set("java")
    inputSpec.set("$projectDir/src/main/resources/cash.json")
    outputDir.set("$projectDir/build/generated")
    modelPackage.set("ru.bank.front.cash.domain")
    invokerPackage.set("ru.bank.front.cash.client")
    apiPackage.set("ru.bank.front.cash.client.api")
    packageName.set("Cash")
    configOptions.set([
            hideGenerationTimestamp: "true",
            library                : "resttemplate",
            useJakartaEe           : "true",
            useTags                : "true",
            openApiNullable        : "false",
            serializableModel      : "true"
    ])
}

compileJava.dependsOn tasks.buildCashClient